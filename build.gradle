buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.19"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.10.0"
    }
}

apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: "com.diffplug.gradle.spotless"

apply from: 'libs.gradle'

group 'com.alisiikh'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    scalaCompilerPlugin
}

dependencies {
    scalaCompilerPlugin libs.macroParadise

    compile libs.scala
    compile libs.simulacrum
    compile libs.catsCore
    compile libs.catsLaws

    testCompile libs.scalaTest
    testRuntime libs.pegdown
}

compileScala {
    configure(scalaCompileOptions) {
        additionalParameters = [
                "-feature",
                "-deprecation",
                "-unchecked",
                "-Xfatal-warnings",
                "-Ypartial-unification",
                "-Xplugin:" + configurations.scalaCompilerPlugin.asPath
        ]
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
}

spotless {
    scala {
        scalafmt('1.4.0').configFile('.scalafmt.conf')
    }
    format 'misc', {
        target '**/*.gradle', '**/*.properties', '**/*.conf'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}
